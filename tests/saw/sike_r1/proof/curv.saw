//curv.saw Copyright 2019 Amazon.com, Inc. or its affiliates. All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

///////////////////////////////////////////////////////////////////////////////
// Montgomery curve operations


///////////////////////////////////////////////////////////////////////////////
// Specifications

let point_op t3 t4 = do {
    (p, pp) <- A_point_t "P";
    qp <- C_point_t();
    (a, ap) <- A_f2elm_t t3;
    (c, cp) <- A_f2elm_t t4;
    crucible_execute_func [pp, qp, ap, cp];
    q <- crucible_fresh_var "q" point_proj_t;
    crucible_points_to qp (tm q);
};

let point_op_same t3 t4 = do {
    (p, pp) <- B_point_t "P";
    (a, ap) <- A_f2elm_t t3;
    (c, cp) <- A_f2elm_t t4;
    crucible_execute_func [pp, pp, ap, cp];
    p' <- crucible_fresh_var "xDBL_p'" point_proj_t;
    crucible_points_to pp (tm p');
};

let point_op_scaled t3 t4 = do {
    (p, pp) <- A_point_t "P";
    qp <- C_point_t();
    (a, ap) <- A_f2elm_t t3;
    (c, cp) <- A_f2elm_t t4;
    e <- A_int_t "e";
    crucible_execute_func [pp, qp, ap, cp, tm e];
    q <- crucible_fresh_var "q" point_proj_t;
    crucible_points_to qp (tm q);
};

let point_op_scaled_same t3 t4 = do {
    (p, pp) <- B_point_t "P";
    (a, ap) <- A_f2elm_t t3;
    (c, cp) <- A_f2elm_t t4;
    e <- A_int_t "e";
    crucible_execute_func [pp, pp, ap, cp, tm e];
    q <- crucible_fresh_var "q" point_proj_t;
    crucible_points_to pp (tm q);
};

// NOTE: this assumes a known number of iterations of xDBLe and xTRLe
let point_op_iter t3 t4 e = do {
    (p, pp) <- A_point_t "P";
    qp <- C_point_t();
    (a, ap) <- A_f2elm_t t3;
    (c, cp) <- A_f2elm_t t4;
    crucible_execute_func [pp, qp, ap, cp, tm e];
    q <- crucible_fresh_var "q" point_proj_t;
    crucible_points_to qp (tm q);
};

let point_op_iter_same t3 t4 e = do {
    (p, pp) <- B_point_t "P";
    (a, ap) <- A_f2elm_t t3;
    (c, cp) <- A_f2elm_t t4;
    crucible_execute_func [pp, pp, ap, cp, tm e];
    q <- crucible_fresh_var "q" point_proj_t;
    crucible_points_to pp (tm q);
};

let point_op_scaled_same_32 t3 t4 = do {
    (p, pp) <- B_point_t "P";
    (a, ap) <- A_f2elm_t t3;
    (c, cp) <- A_f2elm_t t4;
    e <- A_uint_t "e";
    crucible_execute_func [pp, pp, ap, cp, tm e];
    q <- crucible_fresh_var "q" point_proj_t;
    crucible_points_to pp (tm q);
};

let xDBL_spec = point_op "A24plus" "C24";
let xDBL_same_spec = point_op_same "A24plus" "C24";
let xDBLe_spec_known_e = point_op_iter "A24plus" "C24";
let xDBLe_spec = point_op_scaled "A24plus" "C24";
let xDBLe_same_spec = point_op_scaled_same "A24plus" "C24";
let xDBLe_same_spec_known_e = point_op_iter_same "A24plus" "C24";
let xDBLe_same32_spec = point_op_scaled_same_32 "A24plus" "C24";

let xTPL_spec = point_op "A24minus" "A24plus";
let xTPL_same_spec = point_op_same "A24minus" "A24plus";
let xTPLe_spec_known_e = point_op_iter "A24minus" "A24plus";
let xTPLe_spec = point_op_scaled "A24minus" "A24plus";
let xTPLe_same_spec = point_op_scaled_same "A24minus" "A24plus";
let xTPLe_same_spec_known_e = point_op_iter_same "A24minus" "A24plus";
let xTPLe_same32_spec = point_op_scaled_same_32 "A24minus" "A24plus";


///////////////////////////////////////////////////////////////////////////////
// Proof commands

/* NOTES for xDBLe and xTPLe proofs:

 - We cannot prove either function with the "e" parameter a variable
   due to limitations of SAW. Instead we have proved things about them
   for some particular (and smallish) values of "e".

 - There are a fixed number of specific values of "e" that arise from
   the Alice and Bob strategies, so in principle we could prove something
   for every value that a caller needs. This includes a few large "e".
*/

xDBL_ov <- verify "xDBL_r1"
    [ fp2add503_left_ov,fp2add503_ov, fp2mul503_mont_left_ov, fp2mul503_mont_ov
    , fp2sqr503_mont_same_ov, fp2sub503_ov, fp2sub503_left_ov ] 
    xDBL_spec;

xDBL_same_ov <- verify "xDBL_r1"
    [ fp2add503_left_ov,fp2add503_ov, fp2mul503_mont_left_ov, fp2mul503_mont_ov
    , fp2sqr503_mont_same_ov, fp2sub503_ov, fp2sub503_left_ov ]
    xDBL_same_spec;

// NOTE: Proofs for some of the "e" values used in calls
for [1,2,4,5] (\e -> do {
    verify "xDBLe_r1"
        [xDBL_same_ov, copy_words_nwords_field_x4_ov]
        (xDBLe_spec_known_e {{ `e:[32] }});
    });

xDBLe_ov <- admit "xDBLe_r1" [] xDBLe_spec;

// NOTE: Proofs for some of the "e" values used in calls
for [1,2,4,5] (\e -> do {
    verify "xDBLe_r1"
        [xDBL_same_ov, copy_words_same_nwords_field_x4_ov]
        (xDBLe_same_spec_known_e {{ `e:[32] }});
    });

xDBLe_same_ov <- admit "xDBLe_r1" [] xDBLe_same_spec;
xDBLe_same32_ov <- admit "xDBLe_r1" [] xDBLe_same32_spec;

xTPL_ov <- verify "xTPL_r1"
    [ fp2sub503_ov, fp2sqr503_mont_ov, fp2add503_ov, fp2sub503_left_ov
    , fp2sub503_right_ov, fp2mul503_mont_left_ov, fp2mul503_mont_ov
    , fp2sqr503_mont_same_ov, fp2sqr503_mont_ov ]
    xTPL_spec;

xTPL_same_ov <- verify "xTPL_r1"
    [ fp2sub503_ov, fp2sqr503_mont_ov, fp2add503_ov, fp2sub503_left_ov
    , fp2sub503_right_ov, fp2mul503_mont_left_ov, fp2mul503_mont_ov
    , fp2sqr503_mont_same_ov, fp2sqr503_mont_ov ]
    xTPL_same_spec;

// NOTE: Proofs for some of the "e" values used in calls
for [1,2,4,5] (\e -> do {
    verify "xTPLe_r1"
        [xTPL_same_ov, copy_words_nwords_field_x4_ov]
        (xTPLe_spec_known_e {{ `e:[32] }});
    });

xTPLe_ov <- admit "xTPLe_r1" [] xTPLe_spec;

// NOTE: Proofs for some of the "e" values used in calls
for [1,2,4,5] (\e -> do {
    verify "xTPLe_r1"
       [xTPL_same_ov, copy_words_same_nwords_field_x4_ov]
       (xTPLe_same_spec_known_e {{ `e:[32] }});
    });

xTPLe_same_ov <- admit "xTPLe_r1" [] xTPLe_same_spec;
xTPLe_same32_ov <- admit "xTPLe_r1" [] xTPLe_same32_spec;

