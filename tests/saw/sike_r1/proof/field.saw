//field.saw Copyright 2019 Amazon.com, Inc. or its affiliates. All Rights Reserved.
// SPDX-License-Identifier: Apache-2.0

///////////////////////////////////////////////////////////////////////////////
// Verify the field operations


///////////////////////////////////////////////////////////////////////////////
// Specifications

let inplace_ptr_op_felm = do {
    (a, ap) <- B_felm_t "a";
    crucible_execute_func [ap];
    c <- crucible_fresh_var "c" felm_t;
    crucible_points_to ap (tm c);
};

let unary_ptr_op_felm = do {
    (a, ap) <- A_felm_t "a";
    cp <- C_felm_t ();
    crucible_execute_func [ap, cp];
    c <- crucible_fresh_var "c" felm_t;
    crucible_points_to cp (tm c);
};

let unary_ptr_op_felm_same = do {
    (a, ap) <- B_felm_t "a";
    crucible_execute_func [ap, ap];
    c <- crucible_fresh_var "c" felm_t;
    crucible_points_to ap (tm c);
};

let binary_ptr_op_felm = do {
    (a, ap) <- A_felm_t "a";
    (b, bp) <- A_felm_t "b";
    cp <- C_felm_t ();
    crucible_execute_func [ap, bp, cp];
    c <- crucible_fresh_var "c" felm_t;
    crucible_points_to cp (tm c);
};

let binary_ptr_op_felm_left = do {
    (a, ap) <- B_felm_t "a";
    (b, bp) <- A_felm_t "b";
    crucible_execute_func [ap, bp, ap];
    c <- crucible_fresh_var "c" felm_t;
    crucible_points_to ap (tm c);
};

let binary_ptr_op_felm_right = do {
    (a, ap) <- A_felm_t "a";
    (b, bp) <- B_felm_t "b";
    crucible_execute_func [ap, bp, bp];
    c <- crucible_fresh_var "c" felm_t;
    crucible_points_to bp (tm c);
};

let binary_ptr_op_felm_same = do {
    (a, ap) <- B_felm_t "a";
    crucible_execute_func [ap, ap, ap];
    c <- crucible_fresh_var "c" felm_t;
    crucible_points_to ap (tm c);
};

let binary_ptr_op_felm_pair = do {
    (a, ap) <- A_felm_t "a";
    cp <- C_felm_t ();
    crucible_execute_func [ap, ap, cp];
    c <- crucible_fresh_var "c" felm_t;
    crucible_points_to cp (tm c);
};

let inplace_ptr_op_f2elm = do {
    (a, ap) <- B_f2elm_t "a";
    crucible_execute_func [ap];
    c <- crucible_fresh_var "c" f2elm_t;
    crucible_points_to ap (tm c);
};

let unary_ptr_op_f2elm = do {
    (a, ap) <- A_f2elm_t "a";
    cp <- C_f2elm_t ();
    crucible_execute_func [ap, cp];
    c <- crucible_fresh_var "c" f2elm_t;
    crucible_points_to cp (tm c);
};

let unary_ptr_op_f2elm_same = do {
    (a, ap) <- B_f2elm_t "a";
    crucible_execute_func [ap, ap];
    c <- crucible_fresh_var "c" f2elm_t;
    crucible_points_to ap (tm c);
};

let binary_ptr_op_f2elm = do {
    (a, ap) <- A_f2elm_t "a";
    (b, bp) <- A_f2elm_t "b";
    cp <- C_f2elm_t ();
    crucible_execute_func [ap, bp, cp];
    c <- crucible_fresh_var "c" f2elm_t;
    crucible_points_to cp (tm c);
};

let binary_ptr_op_f2elm_left = do {
    (a, ap) <- B_f2elm_t "a";
    (b, bp) <- A_f2elm_t "b";
    crucible_execute_func [ap, bp, ap];
    c <- crucible_fresh_var "c" f2elm_t;
    crucible_points_to ap (tm c);
};

let binary_ptr_op_f2elm_right = do {
    (a, ap) <- A_f2elm_t "a";
    (b, bp) <- B_f2elm_t "b";
    crucible_execute_func [ap, bp, bp];
    c <- crucible_fresh_var "c" f2elm_t;
    crucible_points_to bp (tm c);
};

let binary_ptr_op_f2elm_same = do {
    (a, ap) <- B_f2elm_t "a";
    crucible_execute_func [ap, ap, ap];
    c <- crucible_fresh_var "c" f2elm_t;
    crucible_points_to ap (tm c);
};

let binary_ptr_op_f2elm_pair = do {
    (a, ap) <- B_f2elm_t "a";
    (c, cp) <- B_f2elm_t "c";
    crucible_execute_func [ap, ap, cp];
    c <- crucible_fresh_var "c" f2elm_t;
    crucible_points_to cp (tm c);
};

let fpzero503_spec = do {
    cp <- C_felm_t ();
    crucible_execute_func [cp];
    c <- crucible_fresh_var "c" felm_t;
    crucible_points_to cp (tm c);
};

let fpcopy503_spec = do {
    (a, ap) <- A_felm_t "a";
    cp <- C_felm_t ();
    crucible_execute_func [ap, cp];
    crucible_points_to cp (tm a);
};

let fpcorrection503_spec = inplace_ptr_op_felm;

let fpneg503_spec = inplace_ptr_op_felm;

let fpadd503_spec = binary_ptr_op_felm;
let fpadd503_left_spec = binary_ptr_op_felm_left;
let fpadd503_right_spec = binary_ptr_op_felm_right;
let fpadd503_same_spec = binary_ptr_op_felm_same;
let fpadd503_pair_spec = binary_ptr_op_felm_pair;

let fpsub503_spec = binary_ptr_op_felm;
let fpsub503_left_spec = binary_ptr_op_felm_left;
let fpsub503_right_spec = binary_ptr_op_felm_right;
let fpsub503_same_spec = binary_ptr_op_felm_same;

let fpdiv2_503_spec = unary_ptr_op_felm;
let fpdiv2_503_same_spec = unary_ptr_op_felm_same;

let rdc_mont_spec = do {
    (a, ap) <- A_dfelm_t "a";
    cp <- C_felm_t ();
    crucible_execute_func [ap, cp];
    c <- crucible_fresh_var "c" felm_t;
    crucible_points_to cp (tm c);
};

let fpmul503_mont_spec = binary_ptr_op_felm;
let fpmul503_mont_left_spec = binary_ptr_op_felm_left;
let fpmul503_mont_right_spec = binary_ptr_op_felm_right;
let fpmul503_mont_same_spec = binary_ptr_op_felm_same;

let fpsqr503_mont_spec = unary_ptr_op_felm;
let fpsqr503_mont_same_spec = unary_ptr_op_felm_same;

let fpinv503_chain_mont_spec = inplace_ptr_op_felm;

let fpinv503_mont_spec = inplace_ptr_op_felm;

let to_mont_spec = unary_ptr_op_felm;
let to_mont_same_spec = unary_ptr_op_felm_same;

let from_mont_spec = unary_ptr_op_felm;

let fp2zero503_spec = do {
    cp <- C_f2elm_t ();
    crucible_execute_func [cp];
    c <- crucible_fresh_var "c" f2elm_t;
    crucible_points_to cp (tm c);
};

let fp2copy503_spec = do {
    (a, ap) <- A_f2elm_t "a";
    cp <- C_f2elm_t ();
    crucible_execute_func [ap, cp];
    crucible_points_to cp (tm a);
};

let fp2correction503_spec = inplace_ptr_op_f2elm;

let fp2neg503_spec = inplace_ptr_op_f2elm;

let fp2add503_spec = binary_ptr_op_f2elm;
let fp2add503_left_spec = binary_ptr_op_f2elm_left;
let fp2add503_right_spec = binary_ptr_op_f2elm_right;
let fp2add503_same_spec = binary_ptr_op_f2elm_same;
let fp2add503_pair_spec = binary_ptr_op_f2elm_pair;

let fp2sub503_spec = binary_ptr_op_f2elm;
let fp2sub503_left_spec = binary_ptr_op_f2elm_left;
let fp2sub503_right_spec = binary_ptr_op_f2elm_right;
let fp2sub503_same_spec = binary_ptr_op_f2elm_same;

let fp2div2_503_spec = unary_ptr_op_f2elm;
let fp2div2_503_same_spec = unary_ptr_op_f2elm_same;

let fp2mul503_mont_spec = binary_ptr_op_f2elm;
let fp2mul503_mont_left_spec = binary_ptr_op_f2elm_left;
let fp2mul503_mont_right_spec = binary_ptr_op_f2elm_right;
let fp2mul503_mont_same_spec = binary_ptr_op_f2elm_same;

let fp2sqr503_mont_spec = unary_ptr_op_f2elm;
let fp2sqr503_mont_same_spec = unary_ptr_op_f2elm_same;

let to_fp2mont_spec = unary_ptr_op_f2elm;
let to_fp2mont_same_spec = unary_ptr_op_f2elm_same;

let from_fp2mont_spec = unary_ptr_op_f2elm;

let fp2inv503_mont_spec = inplace_ptr_op_f2elm;


///////////////////////////////////////////////////////////////////////////////
// Proof commands

fpzero503_ov <- verify fpzero_fun_name [] fpzero503_spec;
fpcopy503_ov <- verify fpcopy_fun_name [] fpcopy503_spec;
fpcorrection503_ov <- verify fpcorrection_fun_name [] fpcorrection503_spec;

fpneg503_ov <- verify fpneg_fun_name [] fpneg503_spec;

fpadd503_ov <- verify fpadd_fun_name [] fpadd503_spec;
fpadd503_left_ov <- verify fpadd_fun_name [] fpadd503_left_spec;
fpadd503_right_ov <- verify fpadd_fun_name [] fpadd503_right_spec;
fpadd503_same_ov <- verify fpadd_fun_name [] fpadd503_same_spec;
fpadd503_pair_ov <- verify fpadd_fun_name [] fpadd503_pair_spec;

fpsub503_ov <- verify fpsub_fun_name [] fpsub503_spec;
fpsub503_left_ov <- verify fpsub_fun_name [] fpsub503_left_spec;
fpsub503_right_ov <- verify fpsub_fun_name [] fpsub503_right_spec;
fpsub503_same_ov <- verify fpsub_fun_name [] fpsub503_same_spec;

fpdiv2_503_ov <- verify fpdiv2_fun_name [] fpdiv2_503_spec;
fpdiv2_503_same_ov <- verify fpdiv2_fun_name [] fpdiv2_503_same_spec;

rdc_mont_ov <- verify rdc_mont_fun_name
    [dxd_ov]
    rdc_mont_spec;

fpmul503_mont_ov <- verify fpmul_mont_fun_name
    [rdc_mont_ov,mul_ov]
    fpmul503_mont_spec;
fpmul503_mont_left_ov <- verify fpmul_mont_fun_name
    [rdc_mont_ov,mul_ov]
    fpmul503_mont_left_spec;
fpmul503_mont_right_ov <- verify fpmul_mont_fun_name
    [rdc_mont_ov,mul_ov]
    fpmul503_mont_right_spec;
fpmul503_mont_same_ov <- verify fpmul_mont_fun_name
    [rdc_mont_ov,mul_ov]
    fpmul503_mont_same_spec;

fpsqr503_mont_ov <- verify fpsqr_mont_fun_name
    [mul_single_ov,rdc_mont_ov]
    fpsqr503_mont_spec;
fpsqr503_mont_same_ov <- verify fpsqr_mont_fun_name
    [mul_single_ov,rdc_mont_ov]
    fpsqr503_mont_same_spec;

fpinv503_chain_mont_ov <- verify fpinv_chain_mont_fun_name
    [ fpmul503_mont_ov
    , fpmul503_mont_right_ov
    , fpsqr503_mont_ov
    , fpsqr503_mont_same_ov
    , fpcopy503_ov
    ]
    fpinv503_chain_mont_spec;

fpinv503_mont_ov <- verify fpinv_mont_fun_name
    [ fpcopy503_ov
    , fpinv503_chain_mont_ov
    , fpsqr503_mont_same_ov
    , fpmul503_mont_left_ov
    ]
    fpinv503_mont_spec;

to_mont_ov <- verify to_mont_fun_name
    [fpmul503_mont_ov]
    to_mont_spec;
to_mont_same_ov <- verify to_mont_fun_name
    [fpmul503_mont_left_ov]
    to_mont_same_spec;
from_mont_ov <- verify from_mont_fun_name
    [fpmul503_mont_ov,fpcorrection503_ov]
    from_mont_spec;

fp2copy503_ov <- verify fp2copy_fun_name
    [fpcopy503_ov] fp2copy503_spec;

fp2correction503_ov <- verify fp2correction_fun_name
    [fpcorrection503_ov] fp2correction503_spec;

fp2neg503_ov <- verify fp2neg_fun_name
    [fpneg503_ov]
    fp2neg503_spec;

fp2add503_ov <- verify fp2add_fun_name
    [fpadd503_ov] fp2add503_spec;
fp2add503_left_ov <- verify fp2add_fun_name
    [fpadd503_left_ov] fp2add503_left_spec;
fp2add503_right_ov <- verify fp2add_fun_name
    [fpadd503_right_ov] fp2add503_right_spec;
fp2add503_same_ov <- verify fp2add_fun_name
    [fpadd503_same_ov] fp2add503_same_spec;
fp2add503_pair_ov <- verify fp2add_fun_name
    [fpadd503_pair_ov] fp2add503_pair_spec;

fp2sub503_ov <- verify fp2sub_fun_name
    [fpsub503_ov] fp2sub503_spec;
fp2sub503_left_ov <- verify fp2sub_fun_name
    [fpsub503_left_ov] fp2sub503_left_spec;
fp2sub503_right_ov <- verify fp2sub_fun_name
    [fpsub503_right_ov] fp2sub503_right_spec;
fp2sub503_same_ov <- verify fp2sub_fun_name
    [fpsub503_same_ov] fp2sub503_same_spec;

fp2div2_503_ov <- verify fp2div2_fun_name
    [fpdiv2_503_ov] fp2div2_503_spec;
fp2div2_503_same_ov <- verify fp2div2_fun_name
    [fpdiv2_503_same_ov] fp2div2_503_same_spec;

fp2mul503_mont_ov <- verify fp2mul_mont_fun_name
    [rdc_mont_ov,mul_ov]
    fp2mul503_mont_spec;
fp2mul503_mont_left_ov <- verify fp2mul_mont_fun_name
    [rdc_mont_ov,mul_ov]
    fp2mul503_mont_left_spec;
fp2mul503_mont_right_ov <- verify fp2mul_mont_fun_name
    [rdc_mont_ov,mul_ov]
    fp2mul503_mont_right_spec;
fp2mul503_mont_same_ov <- verify fp2mul_mont_fun_name
    [rdc_mont_ov,mul_ov]
    fp2mul503_mont_same_spec;

fp2sqr503_mont_ov <- verify fp2sqr_mont_fun_name
    [fpmul503_mont_ov]
    fp2sqr503_mont_spec;
fp2sqr503_mont_same_ov <- verify fp2sqr_mont_fun_name
    [fpmul503_mont_ov, fpmul503_mont_right_ov]
    fp2sqr503_mont_same_spec;

fp2inv503_mont_ov <- verify fp2inv_mont_fun_name
    [ fpsqr503_mont_ov
    , fpadd503_left_ov
    , fpinv503_mont_ov
    , fpneg503_ov
    , fpmul503_mont_left_ov
    ]
    fp2inv503_mont_spec;

to_fp2mont_ov <- verify to_fp2mont_fun_name
    [to_mont_ov]
    to_fp2mont_spec;
to_fp2mont_same_ov <- verify to_fp2mont_fun_name
    [to_mont_same_ov]
    to_fp2mont_same_spec;
from_fp2mont_ov <- verify from_fp2mont_fun_name
    [from_mont_ov]
    from_fp2mont_spec;

